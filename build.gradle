buildscript {
  ext {
    kotlinVersion = "1.2.20"
    lombokVersion = "1.16.18"
    slf4jVersion = "1.7.25"
    junitJupiterVersion = "5.0.2"
    junitPlatformVersion = "1.0.2"

    mvnRepoDir = "$project.buildDir/mvn-repo"
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
  }
}

allprojects {
  apply plugin: "base"
  apply plugin: "java"
  apply plugin: "maven"
  apply plugin: "maven-publish"

  group "daggerok"
  version "0.0.1"

  defaultTasks "clean", "build"
}

subprojects {

  apply plugin: "kotlin"

  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

  compileKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
  }

  compileTestKotlin {
    kotlinOptions.jvmTarget = project.targetCompatibility
  }

  repositories {
    mavenCentral()
  }

  dependencies {

    compileOnly("org.projectlombok:lombok:$lombokVersion")

    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
    // @Slf4j
    compile("org.slf4j:slf4j-api:$slf4jVersion")
    compile("org.slf4j:jul-to-slf4j:$slf4jVersion")
    compile("org.slf4j:log4j-over-slf4j:$slf4jVersion")
    compile("ch.qos.logback:logback-classic:1.2.3")

    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
  }

  publishing {
    repositories {
      maven {
        // change to point to your repo, e.g. http://my.org/repo
        url "$mvnRepoDir"
      }
    }
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }
}
